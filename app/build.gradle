import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
}

def keystorePropertiesFile = file("./AndroidKeystore/keystore.properties")
def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 36

    defaultConfig {
        applicationId "dev.namelessnanashi.gboardhook"
        minSdk 23
        targetSdk 36
        versionCode (System.currentTimeMillis() / 1000 as int)
        versionName "1.2.2-localbuild"
    }
    lintOptions {
        disable 'ExpiredTargetSdkVersion'
    }
    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE_FILE") ?: "AndroidKeystore/release-key.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD") ?: ""
            keyAlias System.getenv("KEY_ALIAS") ?: ""
            keyPassword System.getenv("KEY_PASSWORD") ?: ""
        }
    }
    buildTypes {
        debug {
            signingConfig = signingConfigs.release
            minifyEnabled = false
        }
        release {
            signingConfig = signingConfigs.release
            minifyEnabled = true
            proguardFiles = [getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    namespace = 'dev.namelessnanashi.gboardhook'
    buildFeatures {
        buildConfig = true
    }
}

kotlin {
    jvmToolchain(21)
}

androidComponents {
    onVariants(selector().all()) { variant ->
        variant.outputs.forEach { output ->
            def versionName = output.versionName.orElse("unspecified").get()
            def versionCode = output.versionCode.orElse(0).get()

            def buildType = variant.getBuildType()
            def flavor = variant.getFlavorName() ?: ""
            def environment = flavor ? "${flavor}_${buildType}" : buildType

            def date = new Date()
            def sdf = new SimpleDateFormat("yyyyMMdd_HHmmss")
            sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
            def formattedDate = sdf.format(date) + "Z"

            def appName = "GboardHook"
            def apkName = "${appName}_${environment}_${versionName}_${versionCode}_${formattedDate}.apk"

            output.outputFileName.set(apkName)
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'com.google.android.material:material:1.14.0-alpha08'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.3.0-RC3'
    implementation files('../libs/api-82.jar')
    implementation 'org.luckypray:dexkit:2.0.7'
}
