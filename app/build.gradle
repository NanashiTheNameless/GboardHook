import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def keystorePropertiesFile = file("./AndroidKeystore/keystore.properties")
def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "dev.namelessnanashi.gboardhook"
        minSdk 23
        targetSdk 34
        versionCode 9
        versionName "1.2.0"
    }
    lintOptions {
        disable 'ExpiredTargetSdkVersion'
    }
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } else {
                // If the file doesn't exist, provide a default message or throw an exception
                println "Warning: keystore.properties not found, using debug signing."
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            // Get version name and version code
            def versionName = variant.versionName
            def versionCode = variant.versionCode

            // Get buildType and flavor
            def buildType = variant.buildType.name
            def flavor = variant.flavorName ?: "" // If no flavor, then empty
            def environment = flavor ? "${flavor}_${buildType}" : buildType

            // Get current UTC time
            def date = new Date()
            def sdf = new SimpleDateFormat("yyyyMMdd_HHmmss")
            sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
            def formattedDate = sdf.format(date) + "Z"

            // Application name
            def appName = "GboardHook"

            // Construct APK file name
            def apkName = "${appName}_${environment}_${versionName}_${versionCode}_${formattedDate}.apk"

            // Set output file name
            output.outputFileName = apkName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    kotlinOptions {
        jvmTarget = '21'
    }
    namespace 'dev.namelessnanashi.gboardhook'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation files('libs/api-82.jar')
    implementation 'org.luckypray:dexkit:2.0.7'
}